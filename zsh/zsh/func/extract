# extract files into a separate directory automatically

emulate -L zsh

setopt extended_glob
setopt no_clobber
setopt err_return

if [[ -z "$1" ]]; then
    echo "usage: $0 <file>"
    return 1
fi

local tmpdir=zsh-extract-$RANDOM
local archivedir="$(readlink -f ${1:h})"
local archivefile="${1:t}"
local basename=${archivefile:r}
local move=no
local cmd

mkdir $tmpdir

case "$1" in
    *.tar.bz2)
        basename=${basename:r} ;&
    *.tbz2|*.tbz)
        cmd="tar xvj -f '$1' -C $tmpdir"
        ;;
    *.tar.gz)
        basename=${basename:r} ;&
    *.tgz)
        cmd="tar xvz -f '$1' -C $tmpdir"
        ;;
    *.tar.xz|*.tar.lzma)
        basename=${basename:r} ;&
    *.txz)
        cmd="tar xvJ -f '$1' -C $tmpdir"
        ;;
    *.tar)
        cmd="tar xv -f '$1' -C $tmpdir"
        ;;
    *.rar)
        cmd="unrar x '${archivefile}'"
        move=yes
        ;;
    *.lha|*.lzh)
        cmd="lha x '${archivefile}'"
        move=yes
        ;;
    *.7z)
        cmd="7z x -o$tmpdir '$1'"
        ;;
    *.jar|*.war|*.ear|*.xpi|*.zip)
        cmd="unzip -d $tmpdir '$1'"
        ;;
    *.deb)
        cmd="ar -x '${archivefile}'"
        move=yes
        ;;
    *.bz2)
        cmd="bzip2 -d -c '$1' > '$basename'"
        ;;
    *.gz|*.Z)
        cmd="gzip -d -c '$1' > '$basename'"
        ;;
    *.xz|*.lzma)
        cmd="xz -d -c '$1' > '$basename'"
        ;;
    *)
        echo "Unsupported archive type"
        rmdir $tmpdir
        return 1
        ;;
esac

if [[ $move == yes ]]; then
    mv "$1" $tmpdir
    cd $tmpdir
    eval "$cmd"
    local RET=$?
    mv "$archivefile" "$archivedir"
    cd ..
else
    eval "$cmd"
    local RET=$?
fi

if (( $RET != 0 )); then
    rmdir $tmpdir
    return $RET
fi

echo

local numfiles=$(ls $tmpdir | wc -l)
if (( $numfiles == 0 )); then
    rmdir $tmpdir
    echo "Extracted file to \"$basename\""
elif (( $numfiles == 1 )); then
    local file="$(basename $tmpdir/*)"
    if ! [[ -e "$file" ]]; then
        mv "$tmpdir/$file" .
        rmdir $tmpdir
        echo "Extracted \"$file\" to current directory"
    else
        echo "Extracted \"$file\" to directory \"$tmpdir\""
    fi
else
    if ! [[ -e "$basename" ]]; then
        mv $tmpdir "$basename"
        echo "Extracted files to directory \"$basename\""
    else
        echo "Extracted files to directory \"$tmpdir\""
    fi
fi

# vim: filetype=zsh
