# make cd push the old directory onto the directory stack.
setopt auto_pushd

# Don't push multiple copies of the same directory onto the directory stack.
setopt pushd_ignore_dups

# Exchanges  the meanings of `+' and `-' when used with a number to specify a
# directory in the stack.
setopt pushd_minus

# Do not print the directory stack after pushd or popd.
setopt pushd_silent

DIRSTACKSIZE=20
DIRSTACKFILE=$ZSH_VAR_LIB_DIR/zdirs

if [[ -f ${DIRSTACKFILE} ]] && [[ ${#dirstack[*]} -eq 0 ]] ; then
    dirstack=( ${(f)"$(< $DIRSTACKFILE)"} )
    # "cd -" won't work after login by just setting $OLDPWD, so
    is_local=1
    # Check whether the old directory is on a remote filesystem that may hang
    for mount in $(findmnt -o target -t nfs4 -l | tail -n +2); do
        if [[ $dirstack[1] =~ ^${mount}(/.*)?$ ]]; then
            is_local=0
            break
        fi
    done
    if (( is_local )); then
        [[ -d $dirstack[1] ]] && cd $dirstack[1] && cd $OLDPWD
    fi
fi

_update_dirstack() {
    local -ax my_stack
    my_stack=( ${PWD} ${dirstack} )
    if is-at-least 4.2; then
        builtin print -l ${(u)my_stack} >! ${DIRSTACKFILE}
    else
        uprint my_stack >! ${DIRSTACKFILE}
    fi
}
add-zsh-hook chpwd _update_dirstack

# vim: filetype=zsh
