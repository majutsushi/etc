# Adapted from:
# http://www.zsh.org/mla/workers//2015/msg01110.html
# http://mika.l3ib.org/code/dot-zshrc

if [[ -n "$ZSHRUN" ]]; then
    PS1=$'%Bzshrun%b %F{2}%~%f> '

    setopt hist_ignore_all_dups
    unsetopt share_history
    HISTFILE=$ZSH_STATE_DIR/zshrun-history

    unset ZSHRUN

    unsetopt correct
    unsetopt checkjobs
    setopt nohup

    _accept_and_quit() {
        local -a buf
        buf=(${(z)BUFFER})
        if have -c $buf[1]; then
            print -sr -- "${BUFFER}"
            print -s
            zsh -c "env -u SHLVL systemd-cat -t zshrun ${BUFFER}" 0>&- 1>&- 2>&- &|
            exit
        else
            zle -M "Command $buf[1] not found"
        fi
    }
    zle -N _accept_and_quit
    bindkey "^M" _accept_and_quit

    _exit() {
        exit
    }
    zle -N _exit
    bindkey "\e" _exit
fi

# vim: filetype=zsh
