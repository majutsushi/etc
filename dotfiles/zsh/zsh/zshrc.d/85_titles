_update_terminal_title() {
    if [[ -n "$1" ]]; then
        set_terminal_title "${1}"
    elif [[ -n "$SSH_CLIENT" ]]; then
        set_terminal_title "${(%):-%m}"
    else
        set_terminal_title "${(%):-%(4~|â€¦/%3~|%~)}"
    fi
}
add-zsh-hook precmd _update_terminal_title


_simplify_cmd() {
    # get first word of command that doesn't match the pattern
    local pattern="^(*=*|sudo|(auto)?ssh|-*)"
    local cmd="${1[(wr)${pattern}]}"

    # only retain part after last /
    cmd="${cmd//*\//}"

    # if running under ssh append @host
    if [[ -n $SSH_CLIENT ]]; then
        cmd+="@${(%):-%m}"
    fi

    print -n "$cmd"
}

# adapted from
# http://git.madduck.net/v/etc/zsh.git/blob/HEAD:/.zsh/zshrc/85_xtermtitle
_update_exec_titles() {
    # split up the command line
    local -a cmd; cmd=(${(z)3})

    # Construct a command that will output the desired job number.
    case $cmd[1] in
        fg) if (( $#cmd == 1 )); then
                # No arguments, must find the current job
                cmd=(builtin jobs -l %+)
            else
                # Replace the command name, ignore extra args.
                cmd=(builtin jobs -l ${(Q)cmd[1]})
            fi;;
        %*) cmd=(builtin jobs -l ${(Q)cmd[1]});; # Same as "else" above
        *) # Not resuming a job,
            _update_terminal_title "$(_simplify_cmd "$cmd")"
            return;;                        # so we're all done
    esac

    local -A jt; jt=(${(kv)jobtexts})       # Copy jobtexts for subshell

    # Run the command, read its output, and look up the jobtext.
    # Could parse $rest here, but $jobtexts (via $jt) is easier.
    $cmd >>(
        read num rest
        cmd=(${(z)${(e):-\$jt$num}})
        _update_terminal_title "$(_simplify_cmd "$cmd")"
    )
}
add-zsh-hook preexec _update_exec_titles

# vim: filetype=zsh
