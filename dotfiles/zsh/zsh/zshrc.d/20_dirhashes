hash -d doc=/usr/share/doc
hash -d log=/var/log

# From https://github.com/vincentbernat/zshrc/blob/master/rc/bookmarks.zsh
#
# Handle bookmarks. This uses the static named directories feature of
# zsh. Such directories are declared with `hash -d
# name=directory`. Both prompt expansion and completion know how to
# handle them. We populate the hash with directories.

MARKPATH=$ZSH_VAR_LIB_DIR/marks

# Populate the hash
for link ($MARKPATH/*(N@)) {
    hash -d -- ${link:t}=${link:A}
}

# Manage bookmarks
bookmark() {
    [[ -d $MARKPATH ]] || mkdir -p $MARKPATH
    if (( $# == 0 )); then
        # When no arguments are provided, just display existing
        # bookmarks
        for link in $MARKPATH/*(N@); do
            local markname="$fg[green]${link:t}$reset_color"
            local markpath="$fg[blue]${link:A}$reset_color"
            printf "%-30s -> %s\n" $markname $markpath
        done
    else
        # Otherwise, we may want to add a bookmark or delete an
        # existing one.
        local -a delete
        zparseopts -D d=delete
        if (( $+delete[1] )); then
            # With `-d`, we delete an existing bookmark
            command rm $MARKPATH/$1
        else
            # Otherwise, add a bookmark to the current
            # directory. The first argument is the bookmark
            # name. `.` is special and means the bookmark should
            # be named after the current directory.
            local name=$1
            [[ $name == "." ]] && name=${PWD:t}
            ln -s $PWD $MARKPATH/$name
            hash -d -- ${name}=${PWD}
        fi
    fi
}

# vim: filetype=zsh
