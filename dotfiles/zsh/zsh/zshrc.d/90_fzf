FZF_CTRL_T_COMMAND="fd --hidden --exclude .git --type file"
FZF_CTRL_T_OPTS="
    --preview 'bat --plain --color always {}'
    --preview-window 'right,border-left,50%'
    --bind 'ctrl-/:change-preview-window(down,50%,border-top|hidden|)'
    --bind 'ctrl-l:preview:echo {}'
    --bind 'f1:execute(less -+F -f {})'
"

FZF_ALT_C_COMMAND="fd --type directory"

FZF_CTRL_R_OPTS="
    --no-sort
    --preview 'echo {}'
    --preview-window 'bottom,border-rounded,3,hidden'
    --bind 'ctrl-/:toggle-preview'
"

if [[ -f /usr/share/doc/fzf/examples/key-bindings.zsh ]]; then
    source /usr/share/doc/fzf/examples/key-bindings.zsh

    # Make fzf widget accessible even when atuin is used
    bindkey -M emacs '\er' fzf-history-widget
    bindkey -M vicmd '\er' fzf-history-widget
    bindkey -M viins '\er' fzf-history-widget
fi


RECENTDIRS_FILE="$ZSH_VAR_LIB_DIR/recent-dirs"
chpwd_add_recent_dir() {
    if [[ "$PWD" != $HOME ]]; then
        print -P '0\t%D{%s}\t1\t%~' >>| "$RECENTDIRS_FILE"
    fi
}
if [[ "$INCOGNITO" != 1 ]]; then
    add-zsh-hook chpwd chpwd_add_recent_dir
fi

j() {
    # adapted from http://chneukirchen.org/blog/archive/2017/01/zz-a-smart-and-efficient-directory-changer.html
    awk -v ${(%):-now='%D{%s}'} < "$RECENTDIRS_FILE" '
        BEGIN { FS="\t" }
        function r(t, f) {
            age = now - t
            return (age < 3600) ? f * 4 : (age < 86400) ? f * 2 : (age < 604800) ? f / 2 : f / 4
        }
        { f[$4] += $3; if ($2 > l[$4]) l[$4] = $2 }
        END { for (i in f) printf("%d\t%d\t%d\t%s\n", r(l[i], f[i]), l[i], f[i], i) }' | \
            sort -k2 -n -r | head -n 9000 | sort -n -r -o "$RECENTDIRS_FILE"
    cd ${~:-"$(cat "$RECENTDIRS_FILE" | cut -d$'\t' -f4- | fzf --exact --no-sort)"}
}

J() {
    cd "$(hash -d | sed -e 's/^.*=//' | fzf --exact)"
}

v() {
    local viminfo="$HOME/.var/lib/vim/viminfo"

    sed -r -n -e 's/^> //p'  ~/.var/lib/vim/viminfo | tac | while read line; do
        [[ -f "${line/\~/$HOME/}" ]] || continue
        [[ "${line//*\//}" == COMMIT_EDITMSG ]] && continue

        echo $line
    done | fzf
}


# For https://github.com/junegunn/fzf-git.sh
_fzf_git_fzf() {
    fzf \
        --layout=reverse --multi --height=50% --min-height=20 --border \
        --color='header:italic:underline' \
        --preview-window='right,50%,border-left' \
        --bind='ctrl-/:change-preview-window(down,50%,border-top|hidden|)' "$@"
}

# vim: filetype=zsh
